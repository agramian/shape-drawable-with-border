plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.github.dcendents.android-maven" version "1.5"
}
apply plugin: 'com.android.library'

def developerName = 'Abtin Gramian'
def repoName = 'shape-drawable-with-border'
def grpPkg ='com.abtingramian.shapedrawablewithborder'
def grpPkgName = 'ShapeDrawableWithBorder'
def grpPkgVersion ='0.1'
def grpPkgDescShort = "${grpPkgName} ${grpPkgVersion}"
def grpPkgDescLong = "Shape Drawable with Border for Android"
def siteUrl = 'https://github.com/agramian/shape-drawable-with-border'
def gitUrl = "${siteUrl}.git"
def licenseName = 'MIT'
def licenseUrl = "${siteUrl}/blob/master/LICENSE"
def firstPublishYear = '2016'

group =  grpPkg
version = grpPkgVersion

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 1
        versionName grpPkgVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:24.2.1'
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task generateJavaDocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath()
            .join(File.pathSeparator))
}

task generateJavaDocsJar(type: Jar) {
    from generateJavaDocs.destinationDir
    classifier 'javadoc'
}
generateJavaDocsJar.dependsOn generateJavaDocs

install {
    repositories.mavenInstaller {
        pom.project {
            name grpPkgName
            description grpPkgDescLong
            url siteUrl
            inceptionYear firstPublishYear

            packaging 'aar'
            groupId grpPkg
            artifactId grpPkgName
            version grpPkgVersion

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                    distribution 'repo'
                }
            }
            scm {
                connection gitUrl
                url siteUrl

            }
            developers {
                developer {
                    name developerName
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = repoName
        userOrg = user
        licenses = [licenseName]
        vcsUrl = gitUrl
        websiteUrl = siteUrl
        version {
            name = grpPkgVersion
            desc = grpPkgDescShort
            vcsTag = grpPkgVersion
            released  = new Date()
            attributes = ['gradle-plugin': grpPkg]
        }
    }
}

artifacts {
    archives generateJavaDocsJar
    archives generateSourcesJar
}
